# from here:
#
# https://github.com/lefticus/cppbestpractices/blob/master/02-Use_the_Tools_Available.md

function(set_project_warnings project_name)
  set(CLANG_WARNINGS
      -Wall
      -Wextra # reasonable and standard
      -Wshadow # warn the user if a variable declaration shadows one from a parent context
      -Wnon-virtual-dtor # warn the user if a class with virtual functions has a non-virtual
                         # destructor. This helps catch hard to track down memory errors
      -Wunused # warn on anything being unused
      -Woverloaded-virtual # warn if you overload (not override) a virtual function
      -Wpedantic # warn if non-standard C++ is used
      -Wfloat-conversion # warn on type conversions that may lose data
      -Wnull-dereference # warn if a null dereference is detected
      -Wdouble-promotion # warn if float is implicit promoted to double
      -Wno-unknown-pragmas # disable warnings of unknown pragmas
  )

  if(${PROJECT_NAME_UPPERCASE}_WARNINGS_AS_ERRORS)
    set(CLANG_WARNINGS ${CLANG_WARNINGS} -Werror)
    set(MSVC_WARNINGS ${MSVC_WARNINGS} /WX)
  endif()

  set(GCC_WARNINGS
      ${CLANG_WARNINGS} -Wmisleading-indentation # warn if indentation implies blocks where
                                                 # blocks do not exist
      -Wlogical-op # warn about logical operations being used where bitwise were probably wanted
  )

  if(MSVC)
    set(PROJECT_WARNINGS ${MSVC_WARNINGS})
  elseif(CMAKE_CXX_COMPILER_ID MATCHES ".*Clang")
    set(PROJECT_WARNINGS ${CLANG_WARNINGS})
  elseif(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
    set(PROJECT_WARNINGS ${GCC_WARNINGS})
  else()
    message(AUTHOR_WARNING "No compiler warnings set for '${CMAKE_CXX_COMPILER_ID}' compiler.")
  endif()

  target_compile_options(${project_name} INTERFACE ${PROJECT_WARNINGS})

  if(NOT TARGET ${project_name})
    message(
      AUTHOR_WARNING "${project_name} is not a target, thus no compiler warning were added."
    )
  endif()
endfunction()

# set a project_warnings target which can be linked as an dependency to add compiler warnings
add_library(project_warnings INTERFACE)
set_project_warnings(project_warnings)
